// Usage https://github.com/bintray/gradle-bintray-plugin

// To avoid this issue https://github.com/gradle/gradle/issues/1118, which can cause transitive dependencies of your
//   published artifacts not to be included, make sure to apply the java-library plugin to your build script.
// See also https://docs.gradle.org/current/userguide/java_library_plugin.html for configuration of java-library plugin
apply plugin:'java'
apply plugin:'java-library'
// maven publish is required for gradle-bintray-plugin
apply plugin: 'maven-publish'

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            // You need the sourcesJar in case you'd like your package to be linked to JCenter
            artifact sourcesJar
            groupId 'dk.danskespil.gradle.plugins'
            artifactId "${project.name}"
            version "${project.version}"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'org.jwermuth'
        name = "${project.name}"
        // optional. Only if repo belongs to an org
        //userOrg = 'jwermuth'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/jwermuth/gradle-plugin-test-spock-helpers.git'
        version {
            name = "${project.version}"
            vcsTag = "${project.version}"
        }
    }
}
